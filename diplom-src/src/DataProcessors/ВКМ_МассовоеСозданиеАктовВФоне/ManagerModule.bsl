
&НаСервере
Функция ЗаполнитьНаСервере(Параметры, АдресРезультата) Экспорт 
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор
	|ПОМЕСТИТЬ втДоговоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	|	И &ДатаДоговора МЕЖДУ ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействияДоговора И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействияДоговора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Договор.Ссылка КАК Реализации
	|ПОМЕСТИТЬ втРеализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Договор.ВидДоговора = &ВидДоговора
	|	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДоговоры.Договор.Ссылка КАК ДоговорСсылка
	|ИЗ
	|	втДоговоры КАК втДоговоры
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеализации КАК втРеализации
	|		ПО (втРеализации.Реализации.Ссылка = втДоговоры.Договор.Ссылка)
	|ГДЕ
	|	втРеализации.Реализации.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ВидДоговора", ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание"));
	Запрос.УстановитьПараметр("НачалоПериода", Параметры.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", Параметры.КонецПериода);
	Запрос.УстановитьПараметр("ДатаДоговора", Параметры.ДатаДоговора);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	МассивДокументов = Новый Массив;
	
	Пока РезультатЗапроса.Следующий() Цикл
		
		СписокДокументов = Новый Структура;
		
		НоваяРеализация = СозданиеРеализации(РезультатЗапроса.ДоговорСсылка,Параметры.КонецПериода);
		СписокДокументов.Вставить("Договор",РезультатЗапроса.ДоговорСсылка);
		СписокДокументов.Вставить("Реализация", НоваяРеализация);
		
		МассивДокументов.Добавить(СписокДокументов);
				
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(МассивДокументов, АдресРезультата);
	Возврат МассивДокументов;
	
КонецФункции

&НаСервере
Функция СозданиеРеализации(Договор, КонецПериода)
		
	НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
	НовыйДокумент.Контрагент = Договор.Владелец;
	НовыйДокумент.Договор = Договор;
	НовыйДокумент.Организация = Договор.Организация;
	НовыйДокумент.Ответственный = ПараметрыСеанса.ТекущийПользователь;
	НовыйДокумент.Дата = КонецПериода;
	НовыйДокумент.ВыполнитьАвтозаполнение();
	
	Если ЗначениеЗаполнено(НовыйДокумент.Услуги) Тогда 
		
		НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный);
		Возврат НовыйДокумент.Ссылка;
			
	КонецЕсли;
		
КонецФункции
