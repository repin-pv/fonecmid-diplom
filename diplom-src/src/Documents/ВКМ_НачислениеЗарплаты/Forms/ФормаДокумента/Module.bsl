
#Область  ОбработчикиКомандФормы

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Объект.ОсновныеНачисления.Очистить();
	Объект.ДополнительныеНачисления.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_Сотрудник КАК Сотрудник,
	               |	ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеПриход КАК СуммаКОплатеПриход
	               |ИЗ
	               |	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВКМ_ФизическиеЛица КАК ВКМ_ФизическиеЛица
	               |		ПО ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_Сотрудник = ВКМ_ФизическиеЛица.Ссылка
	               |ГДЕ
	               |	ВКМ_ФизическиеЛица.Подразделение = &Подразделение
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВКМ_ФизическиеЛица.Ссылка КАК Сотрудник
	               |ИЗ
	               |	Справочник.ВКМ_ФизическиеЛица КАК ВКМ_ФизическиеЛица
	               |ГДЕ
	               |	ВКМ_ФизическиеЛица.Подразделение = &Подразделение"; 
	
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("НачалоПериода", (НачалоМесяца(КонецДня(Объект.Дата))));
	Запрос.УстановитьПараметр("КонецПериода", (КонецМесяца(КонецДня(Объект.Дата))));
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ВыборкаДополнительныеНачисления = МассивРезультатов[0].Выбрать();
	ВыборкаОсновныеНачисления = МассивРезультатов[1].Выбрать();
	
	Пока ВыборкаДополнительныеНачисления.Следующий() Цикл
		
		НоваяСтрока = Объект.ДополнительныеНачисления.Добавить();
		НоваяСтрока.Сотрудник = ВыборкаДополнительныеНачисления.Сотрудник;
		НоваяСтрока.СуммаНачисления = ВыборкаДополнительныеНачисления.СуммаКОплатеПриход;
		НоваяСтрока.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ВКМ_ПремияОтПродаж;
		
	КонецЦикла; 
	
	Пока ВыборкаОсновныеНачисления.Следующий() Цикл
		
		НоваяСтрока = Объект.ОсновныеНачисления.Добавить();
		НоваяСтрока.Сотрудник = ВыборкаОсновныеНачисления.Сотрудник;
		
	КонецЦикла; 		
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда 
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Заполните подразделение. Продолжение не возможно");
		Возврат;
		
	Иначе 
		
		ЗаполнитьНаСервере();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОсновныеНачисленияВидРасчетаПриИзменении(Элемент)
	
	// repin_pv 28.01.24 {{{ Я решил, что удобнее при начсилении Оклада - Заполняем период текущим месяцем
	ПериодНачисления = Элементы.ОсновныеНачисления.ТекущиеДанные;
	Оклад = ПредопределенноеЗначение("ПланВидовРасчета.ВКМ_ОсновныеНачисления.Оклад");
	
	Если ПериодНачисления.ВидРасчета = Оклад Тогда
		
		ПериодНачисления.ДатаНачала = НачалоМесяца(НачалоДня(Объект.Дата));
		ПериодНачисления.ДатаОкончания = КонецМесяца(КонецДня(Объект.Дата));
		
	КонецЕсли;
	// }}} repin_pv 28.01.24 
	
КонецПроцедуры

#КонецОбласти

